#include common_scripts\utility;
#include maps\mp\_utility;
#include maps\mp\gametypes\_hud_util;
#include maps\mp\bots\_bot_utility;

/*
	Initiates the whole bot scripts.
*/

precache_zombie_models()
{
    // Bodies
    precacheModel("char_ger_ansel_body_zomb");
    precacheModel("char_ger_honorgd_body2_1");
    precacheModel("char_ger_honorgd_body1_1");
	precacheModel("char_ger_zombie_body");

    // Zombie Heads - Series 1
    precacheModel("char_ger_honorgd_zombiehead1_1");
    precacheModel("char_ger_honorgd_zombiehead1_2");
    precacheModel("char_ger_honorgd_zombiehead1_3");
    precacheModel("char_ger_honorgd_zombiehead1_4");
    precacheModel("char_ger_honorgd_zombiehead1_5");
    precacheModel("char_ger_honorgd_zombiehead1_6");

    // Zombie Heads - Series 2
    precacheModel("char_ger_honorgd_zombiehead2_1");
    precacheModel("char_ger_honorgd_zombiehead2_2");
    precacheModel("char_ger_honorgd_zombiehead2_3");
    precacheModel("char_ger_honorgd_zombiehead2_4");
    precacheModel("char_ger_honorgd_zombiehead2_5");
    precacheModel("char_ger_honorgd_zombiehead2_6");

    // Zombie Heads - Series 3
    precacheModel("char_ger_honorgd_zombiehead3_1");
    precacheModel("char_ger_honorgd_zombiehead3_2");
    precacheModel("char_ger_honorgd_zombiehead3_3");
    precacheModel("char_ger_honorgd_zombiehead3_4");
    precacheModel("char_ger_honorgd_zombiehead3_5");
    precacheModel("char_ger_honorgd_zombiehead3_6");

    // Zombie Heads - Series 4
    precacheModel("char_ger_honorgd_zombiehead4_1");
    precacheModel("char_ger_honorgd_zombiehead4_2");
    precacheModel("char_ger_honorgd_zombiehead4_3");
    precacheModel("char_ger_honorgd_zombiehead4_4");
    precacheModel("char_ger_honorgd_zombiehead4_5");
    precacheModel("char_ger_honorgd_zombiehead4_6");

    // Ansel Head
    precacheModel("char_ger_ansel_head_zomb");
}

init_zombie_health_scaling()
{
    level.zombie_base_health = 25; // Starting health on match start
    level.zombie_max_health  = 400; // Hard cap so they don't become unkillable
    level.zombie_health_inc  = 25;   // Health added per interval
    level.zombie_scale_time  = 60;   // Increase health every 60 seconds

    level thread monitor_zombie_health_scaling();
}

monitor_zombie_health_scaling()
{
    level endon( "game_ended" );

    for ( ;; )
    {
        wait level.zombie_scale_time;

        if ( level.zombie_base_health < level.zombie_max_health )
        {
            level.zombie_base_health += level.zombie_health_inc;
            
            // Debug notify (Optional)
            iPrintLn( "^7Zombies evolved! Health is now: " + level.zombie_base_health );
        }
    }
}

init()
{
	level.bw_version = "2.3.0";
	
	precache_zombie_models();

	if ( getdvar( "bots_main" ) == "" )
	{
		setdvar( "bots_main", true );
	}
	
	if ( !getdvarint( "bots_main" ) )
	{
		return;
	}
	
	if ( !wait_for_builtins() )
	{
		println( "FATAL: NO BUILT-INS FOR BOTS" );
	}
	
	thread load_waypoints();
	cac_init_patch();
	thread hook_callbacks();
	
	if ( getdvar( "bots_main_GUIDs" ) == "" )
	{
		setdvar( "bots_main_GUIDs", "" ); // guids of players who will be given host powers, comma seperated
	}
	
	if ( getdvar( "bots_main_firstIsHost" ) == "" )
	{
		setdvar( "bots_main_firstIsHost", false ); // first player to connect is a host
	}
	
	if ( getdvar( "bots_main_waitForHostTime" ) == "" )
	{
		setdvar( "bots_main_waitForHostTime", 10.0 ); // how long to wait to wait for the host player
	}
	
	if ( getdvar( "bots_main_kickBotsAtEnd" ) == "" )
	{
		setdvar( "bots_main_kickBotsAtEnd", false ); // kicks the bots at game end
	}
	
	if ( getdvar( "bots_manage_add" ) == "" )
	{
		setdvar( "bots_manage_add", 0 ); // amount of bots to add to the game
	}
	
	if ( getdvar( "bots_manage_fill" ) == "" )
	{
		setdvar( "bots_manage_fill", 0 ); // amount of bots to maintain
	}
	
	if ( getdvar( "bots_manage_fill_spec" ) == "" )
	{
		setdvar( "bots_manage_fill_spec", true ); // to count for fill if player is on spec team
	}
	
	if ( getdvar( "bots_manage_fill_mode" ) == "" )
	{
		setdvar( "bots_manage_fill_mode", 0 ); // fill mode, 0 adds everyone, 1 just bots, 2 maintains at maps, 3 is 2 with 1
	}
	
	if ( getdvar( "bots_manage_fill_kick" ) == "" )
	{
		setdvar( "bots_manage_fill_kick", false ); // kick bots if too many
	}
	
	if ( getdvar( "bots_manage_fill_watchplayers" ) == "" )
	{
		setdvar( "bots_manage_fill_watchplayers", false ); // add bots when player exists, kick if not
	}
	
	if ( getdvar( "bots_team" ) == "" )
	{
		setdvar( "bots_team", "autoassign" ); // which team for bots to join
	}
	
	if ( getdvar( "bots_team_amount" ) == "" )
	{
		setdvar( "bots_team_amount", 0 ); // amount of bots on axis team
	}
	
	if ( getdvar( "bots_team_force" ) == "" )
	{
		setdvar( "bots_team_force", false ); // force bots on team
	}
	
	if ( getdvar( "bots_team_mode" ) == "" )
	{
		setdvar( "bots_team_mode", 0 ); // counts just bots when 1
	}
	
	if ( getdvar( "bots_skill" ) == "" )
	{
		setdvar( "bots_skill", 0 ); // 0 is random, 1 is easy 7 is hard, 8 is custom, 9 is completely random
	}
	
	if ( getdvar( "bots_skill_axis_hard" ) == "" )
	{
		setdvar( "bots_skill_axis_hard", 0 ); // amount of hard bots on axis team
	}
	
	if ( getdvar( "bots_skill_axis_med" ) == "" )
	{
		setdvar( "bots_skill_axis_med", 0 );
	}
	
	if ( getdvar( "bots_skill_allies_hard" ) == "" )
	{
		setdvar( "bots_skill_allies_hard", 0 );
	}
	
	if ( getdvar( "bots_skill_allies_med" ) == "" )
	{
		setdvar( "bots_skill_allies_med", 0 );
	}
	
	if ( getdvar( "bots_skill_min" ) == "" )
	{
		setdvar( "bots_skill_min", 1 );
	}
	
	if ( getdvar( "bots_skill_max" ) == "" )
	{
		setdvar( "bots_skill_max", 7 );
	}
	
	if ( getdvar( "bots_loadout_reasonable" ) == "" ) // filter out the bad 'guns' and perks
	{
		setdvar( "bots_loadout_reasonable", false );
	}
	
	if ( getdvar( "bots_loadout_allow_op" ) == "" ) // allows jug, marty and laststand
	{
		setdvar( "bots_loadout_allow_op", true );
	}
	
	if ( getdvar( "bots_loadout_rank" ) == "" ) // what rank the bots should be around, -1 is around the players, 0 is all random
	{
		setdvar( "bots_loadout_rank", -1 );
	}
	
	if ( getdvar( "bots_loadout_prestige" ) == "" ) // what pretige the bots will be, -1 is the players, -2 is random
	{
		setdvar( "bots_loadout_prestige", -1 );
	}
	
	if ( getdvar( "bots_play_move" ) == "" ) // bots move
	{
		setdvar( "bots_play_move", true );
	}
	
	if ( getdvar( "bots_play_knife" ) == "" ) // bots knife
	{
		setdvar( "bots_play_knife", true );
	}
	
	if ( getdvar( "bots_play_fire" ) == "" ) // bots fire
	{
		setdvar( "bots_play_fire", false );
	}
	
	if ( getdvar( "bots_play_nade" ) == "" ) // bots grenade
	{
		setdvar( "bots_play_nade", true );
	}
	
	if ( getdvar( "bots_play_obj" ) == "" ) // bots play the obj
	{
		setdvar( "bots_play_obj", true );
	}
	
	if ( getdvar( "bots_play_camp" ) == "" ) // bots camp and follow
	{
		setdvar( "bots_play_camp", true );
	}
	
	if ( getdvar( "bots_play_jumpdrop" ) == "" ) // bots jump and dropshot
	{
		setdvar( "bots_play_jumpdrop", true );
	}
	
	if ( getdvar( "bots_play_target_other" ) == "" ) // bot target non play ents (vehicles)
	{
		setdvar( "bots_play_target_other", true );
	}
	
	if ( getdvar( "bots_play_killstreak" ) == "" ) // bot use killstreaks
	{
		setdvar( "bots_play_killstreak", true );
	}
	
	if ( getdvar( "bots_play_ads" ) == "" ) // bot ads
	{
		setdvar( "bots_play_ads", true );
	}
	
	if ( getdvar( "bots_play_aim" ) == "" )
	{
		setdvar( "bots_play_aim", true );
	}
	
	if ( !isdefined( game[ "botWarfare" ] ) )
	{
		game[ "botWarfare" ] = true;
		game[ "botWarfareInitTime" ] = gettime();
	}

	init_zombie_health_scaling();
	
	level.bot_inittime = gettime();
	
	level.defuseobject = undefined;
	level.bots_smokelist = List();
	level.tbl_perkdata[ 0 ][ "reference_full" ] = true;
	
	for ( h = 1; h < 6; h++ )
	{
		for ( i = 0; i < 3; i++ )
		{
			level.default_perk[ "CLASS_CUSTOM" + h ][ i ] = "specialty_null";
		}
	}
	
	level.bots_minsprintdistance = 315;
	level.bots_minsprintdistance *= level.bots_minsprintdistance;
	level.bots_mingrenadedistance = 256;
	level.bots_mingrenadedistance *= level.bots_mingrenadedistance;
	level.bots_maxgrenadedistance = 1024;
	level.bots_maxgrenadedistance *= level.bots_maxgrenadedistance;
	level.bots_maxknifedistance = 128;
	level.bots_maxknifedistance *= level.bots_maxknifedistance;
	level.bots_goaldistance = 27.5;
	level.bots_goaldistance *= level.bots_goaldistance;
	level.bots_noadsdistance = 200;
	level.bots_noadsdistance *= level.bots_noadsdistance;
	level.bots_maxshotgundistance = 500;
	level.bots_maxshotgundistance *= level.bots_maxshotgundistance;
	level.bots_listendist = 100;
	
	level.smokeradius = 255;
	
	level.bots = [];
	
	level.bots_fullautoguns = [];
	level.bots_fullautoguns[ "thompson" ] = false;
	level.bots_fullautoguns[ "mp40" ] = false;
	level.bots_fullautoguns[ "type100smg" ] = false;
	level.bots_fullautoguns[ "ppsh" ] = false;
	level.bots_fullautoguns[ "stg44" ] = false;
	level.bots_fullautoguns[ "30cal" ] = false;
	level.bots_fullautoguns[ "mg42" ] = false;
	level.bots_fullautoguns[ "dp28" ] = false;
	level.bots_fullautoguns[ "bar" ] = false;
	level.bots_fullautoguns[ "fg42" ] = false;
	level.bots_fullautoguns[ "type99lmg" ] = false;
	
	level thread fixGamemodes();
	level thread onUAVAlliesUpdate();
	level thread onUAVAxisUpdate();
	
	level thread onPlayerConnect();
	level thread handleBots();
	level thread onPlayerChat();
	
	array_thread( getentarray( "misc_turret", "classname" ), ::turret_monitoruse_watcher );
}

/*
	Starts the threads for bots.
*/
handleBots()
{
	level thread teamBots();
	level thread diffBots();
	level addBots();
	
	while ( !level.intermission )
	{
		wait 0.05;
	}
	
	setdvar( "bots_manage_add", getBotArray().size );
	
	if ( !getdvarint( "bots_main_kickBotsAtEnd" ) )
	{
		return;
	}
	
	bots = getBotArray();
	
	for ( i = 0; i < bots.size; i++ )
	{
		kick( bots[ i ] getentitynumber() );
	}
}

/*
	The hook callback for when any player becomes damaged.
*/
onPlayerDamage( eInflictor, eAttacker, iDamage, iDFlags, sMeansOfDeath, sWeapon, vPoint, vDir, sHitLoc, timeOffset )
{
	if ( self is_bot() )
	{
		self maps\mp\bots\_bot_internal::onDamage( eInflictor, eAttacker, iDamage, iDFlags, sMeansOfDeath, sWeapon, vPoint, vDir, sHitLoc, timeOffset );
		self maps\mp\bots\_bot_script::onDamage( eInflictor, eAttacker, iDamage, iDFlags, sMeansOfDeath, sWeapon, vPoint, vDir, sHitLoc, timeOffset );
	}
	
	self [[ level.prevcallbackplayerdamage ]]( eInflictor, eAttacker, iDamage, iDFlags, sMeansOfDeath, sWeapon, vPoint, vDir, sHitLoc, timeOffset );
}

/*
	The hook callback when any player gets killed.
*/
onPlayerKilled( eInflictor, eAttacker, iDamage, sMeansOfDeath, sWeapon, vDir, sHitLoc, timeOffset, deathAnimDuration )
{
	if ( self is_bot() )
	{
		self maps\mp\bots\_bot_internal::onKilled( eInflictor, eAttacker, iDamage, sMeansOfDeath, sWeapon, vDir, sHitLoc, timeOffset, deathAnimDuration );
		self maps\mp\bots\_bot_script::onKilled( eInflictor, eAttacker, iDamage, sMeansOfDeath, sWeapon, vDir, sHitLoc, timeOffset, deathAnimDuration );
	}
	
	self.lastattacker = eAttacker;
	
	if ( isdefined( eAttacker ) )
	{
		eAttacker.lastkilledplayer = self;
		eAttacker notify( "killed_enemy" );
	}
	
	self [[ level.prevcallbackplayerkilled ]]( eInflictor, eAttacker, iDamage, sMeansOfDeath, sWeapon, vDir, sHitLoc, timeOffset, deathAnimDuration );
}

/*
	Starts the callbacks.
*/
hook_callbacks()
{
	wait 0.05;
	level.prevcallbackplayerdamage = level.callbackplayerdamage;
	level.callbackplayerdamage = ::onPlayerDamage;
	
	level.prevcallbackplayerkilled = level.callbackplayerkilled;
	level.callbackplayerkilled = ::onPlayerKilled;
}

/*
	Adds the level.radio object for koth. Cause the iw3 script doesn't have it.
*/
fixKoth()
{
	level.radio = undefined;
	
	for ( ;; )
	{
		wait 0.05;
		
		if ( !isdefined( level.radioobject ) )
		{
			continue;
		}
		
		for ( i = level.radios.size - 1; i >= 0; i-- )
		{
			if ( level.radioobject != level.radios[ i ].gameobject )
			{
				continue;
			}
			
			level.radio = level.radios[ i ];
			break;
		}
		
		while ( isdefined( level.radioobject ) && level.radio.gameobject == level.radioobject )
		{
			wait 0.05;
		}
	}
}

/*
	Fixes gamemodes when level starts.
*/
fixGamemodes()
{
	for ( i = 0; i < 19; i++ )
	{
		if ( isdefined( level.bombzones ) && level.gametype == "sd" )
		{
			for ( i = 0; i < level.bombzones.size; i++ )
			{
				level.bombzones[ i ].onuse = ::onUsePlantObjectFix;
			}
			
			break;
		}
		
		if ( isdefined( level.radios ) && level.gametype == "koth" )
		{
			level thread fixKoth();
			
			break;
		}
		
		wait 0.05;
	}
}

/*
	Thread when any player connects. Starts the threads needed.
*/
onPlayerConnect()
{
	for ( ;; )
	{
		level waittill( "connected", player );
		
		player thread onGrenadeFire();
		player thread onWeaponFired();
		player thread doPlayerModelFix();
		player thread onPlayerSpawned();
		
		player thread connected();
	}
}

/*
	Fixes bots perks showing up in killcams and prevents bots from being kicked from old iw3 gsc script.
*/
fixPerksAndScriptKick()
{
	self endon( "disconnect" );
	
	self waittill( "spawned" );
	
	self.pers[ "isBot" ] = undefined;
	
	if ( !level.gameended )
	{
		level waittill ( "game_ended" );
	}
	
	self.pers[ "isBot" ] = true;
}

/*
	When a bot disconnects.
*/
onDisconnectPlayer()
{
	name = self.name;
	
	self waittill( "disconnect" );
	waittillframeend;
	
	for ( i = 0; i < level.bots.size; i++ )
	{
		bot = level.bots[ i ];
		bot BotNotifyBotEvent( "connection", "disconnected", self, name );
	}
}

/*
	When a bot disconnects.
*/
onDisconnect()
{
	self waittill( "disconnect" );
	
	level.bots = array_remove( level.bots, self );
}

/*
	Called when a player connects.
*/
connected()
{
	self endon( "disconnect" );
	
	for ( i = 0; i < level.bots.size; i++ )
	{
		bot = level.bots[ i ];
		bot BotNotifyBotEvent( "connection", "connected", self, self.name );
	}
	
	self thread onDisconnectPlayer();
	
	if ( !isdefined( self.pers[ "bot_host" ] ) )
	{
		self thread doHostCheck();
	}
	
	if ( !self is_bot() )
	{
		return;
	}
	
	if ( !isdefined( self.pers[ "isBot" ] ) )
	{
		// fast restart...
		self.pers[ "isBot" ] = true;
	}
	
	if ( !isdefined( self.pers[ "isBotWarfare" ] ) )
	{
		self.pers[ "isBotWarfare" ] = true;
		self thread added();
	}
	
	self thread fixPerksAndScriptKick();
	
	self thread maps\mp\bots\_bot_internal::connected();
	self thread maps\mp\bots\_bot_script::connected();
	
	level.bots[ level.bots.size ] = self;
	self thread onDisconnect();
	self thread watchBotDebugEvent();

	waittillframeend; // wait for waittills to process
	level notify( "bot_connected", self );
}

/*
	DEBUG
*/
watchBotDebugEvent()
{
	self endon( "disconnect" );
	
	for ( ;; )
	{
		self waittill( "bot_event", msg, str, b, c, d, e, f, g );
		
		if ( getdvarint( "bots_main_debug" ) >= 2 )
		{
			big_str = "Bot Warfare debug: " + self.name + ": " + msg;
			
			if ( isdefined( str ) && isstring( str ) )
			{
				big_str += ", " + str;
			}
			
			if ( isdefined( b ) && isstring( b ) )
			{
				big_str += ", " + b;
			}
			
			if ( isdefined( c ) && isstring( c ) )
			{
				big_str += ", " + c;
			}
			
			if ( isdefined( d ) && isstring( d ) )
			{
				big_str += ", " + d;
			}
			
			if ( isdefined( e ) && isstring( e ) )
			{
				big_str += ", " + e;
			}
			
			if ( isdefined( f ) && isstring( f ) )
			{
				big_str += ", " + f;
			}
			
			if ( isdefined( g ) && isstring( g ) )
			{
				big_str += ", " + g;
			}
			
			BotBuiltinPrintConsole( big_str );
		}
		else if ( msg == "debug" && getdvarint( "bots_main_debug" ) )
		{
			BotBuiltinPrintConsole( "Bot Warfare debug: " + self.name + ": " + str );
		}
	}
}

/*
	When a bot gets added into the game.
*/
added()
{
	self endon( "disconnect" );
	
	self thread maps\mp\bots\_bot_internal::added();
	self thread maps\mp\bots\_bot_script::added();
}

/*
	Adds a bot to the game.
*/
add_bot()
{
	bot = addtestclient();
	
	if ( isdefined( bot ) )
	{
		bot.pers[ "isBot" ] = true;
		bot.pers[ "isBotWarfare" ] = true;
		bot thread added();
	}
}

/*
	A server thread for monitoring all bot's difficulty levels for custom server settings.
*/
diffBots_loop()
{
	var_allies_hard = getdvarint( "bots_skill_allies_hard" );
	var_allies_med = getdvarint( "bots_skill_allies_med" );
	var_axis_hard = getdvarint( "bots_skill_axis_hard" );
	var_axis_med = getdvarint( "bots_skill_axis_med" );
	var_skill = getdvarint( "bots_skill" );
	
	allies_hard = 0;
	allies_med = 0;
	axis_hard = 0;
	axis_med = 0;
	
	if ( var_skill == 8 )
	{
		playercount = level.players.size;
		
		for ( i = 0; i < playercount; i++ )
		{
			player = level.players[ i ];
			
			if ( !isdefined( player.pers[ "team" ] ) )
			{
				continue;
			}
			
			if ( !player is_bot() )
			{
				continue;
			}
			
			if ( player.pers[ "team" ] == "axis" )
			{
				if ( axis_hard < var_axis_hard )
				{
					axis_hard++;
					player.pers[ "bots" ][ "skill" ][ "base" ] = 7;
				}
				else if ( axis_med < var_axis_med )
				{
					axis_med++;
					player.pers[ "bots" ][ "skill" ][ "base" ] = 4;
				}
				else
				{
					player.pers[ "bots" ][ "skill" ][ "base" ] = 1;
				}
			}
			else if ( player.pers[ "team" ] == "allies" )
			{
				if ( allies_hard < var_allies_hard )
				{
					allies_hard++;
					player.pers[ "bots" ][ "skill" ][ "base" ] = 7;
				}
				else if ( allies_med < var_allies_med )
				{
					allies_med++;
					player.pers[ "bots" ][ "skill" ][ "base" ] = 4;
				}
				else
				{
					player.pers[ "bots" ][ "skill" ][ "base" ] = 1;
				}
			}
		}
	}
	else if ( var_skill != 0 && var_skill != 9 )
	{
		playercount = level.players.size;
		
		for ( i = 0; i < playercount; i++ )
		{
			player = level.players[ i ];
			
			if ( !player is_bot() )
			{
				continue;
			}
			
			player.pers[ "bots" ][ "skill" ][ "base" ] = var_skill;
		}
	}
	
	playercount = level.players.size;
	min_diff = getdvarint( "bots_skill_min" );
	max_diff = getdvarint( "bots_skill_max" );
	
	for ( i = 0; i < playercount; i++ )
	{
		player = level.players[ i ];
		
		if ( !player is_bot() )
		{
			continue;
		}
		
		player.pers[ "bots" ][ "skill" ][ "base" ] = int( clamp( player.pers[ "bots" ][ "skill" ][ "base" ], min_diff, max_diff ) );
	}
}

/*
	A server thread for monitoring all bot's difficulty levels for custom server settings.
*/
diffBots()
{
	for ( ;; )
	{
		wait 1.5;
		
		diffBots_loop();
	}
}

/*
	A server thread for monitoring all bot's teams for custom server settings.
*/
teamBots_loop()
{
	teamAmount = getdvarint( "bots_team_amount" );
	toTeam = getdvar( "bots_team" );
	
	alliesbots = 0;
	alliesplayers = 0;
	axisbots = 0;
	axisplayers = 0;
	
	playercount = level.players.size;
	
	for ( i = 0; i < playercount; i++ )
	{
		player = level.players[ i ];
		
		if ( !isdefined( player.pers[ "team" ] ) )
		{
			continue;
		}
		
		if ( player is_bot() )
		{
			if ( player.pers[ "team" ] == "allies" )
			{
				alliesbots++;
			}
			else if ( player.pers[ "team" ] == "axis" )
			{
				axisbots++;
			}
		}
		else
		{
			if ( player.pers[ "team" ] == "allies" )
			{
				alliesplayers++;
			}
			else if ( player.pers[ "team" ] == "axis" )
			{
				axisplayers++;
			}
		}
	}
	
	allies = alliesbots;
	axis = axisbots;
	
	if ( !getdvarint( "bots_team_mode" ) )
	{
		allies += alliesplayers;
		axis += axisplayers;
	}
	
	if ( toTeam != "custom" )
	{
		if ( getdvarint( "bots_team_force" ) )
		{
			if ( toTeam == "autoassign" )
			{
				if ( abs( axis - allies ) > 1 )
				{
					toTeam = "axis";
					
					if ( axis > allies )
					{
						toTeam = "allies";
					}
				}
			}
			
			if ( toTeam != "autoassign" )
			{
				playercount = level.players.size;
				
				for ( i = 0; i < playercount; i++ )
				{
					player = level.players[ i ];
					
					if ( !isdefined( player.pers[ "team" ] ) )
					{
						continue;
					}
					
					if ( !player is_bot() )
					{
						continue;
					}
					
					if ( player.pers[ "team" ] == toTeam )
					{
						continue;
					}
					
					if ( toTeam == "allies" )
					{
						player thread [[ level.allies ]]();
					}
					else if ( toTeam == "axis" )
					{
						player thread [[ level.axis ]]();
					}
					else
					{
						player thread [[ level.spectator ]]();
					}
					
					break;
				}
			}
		}
	}
	else
	{
		playercount = level.players.size;
		
		for ( i = 0; i < playercount; i++ )
		{
			player = level.players[ i ];
			
			if ( !isdefined( player.pers[ "team" ] ) )
			{
				continue;
			}
			
			if ( !player is_bot() )
			{
				continue;
			}
			
			if ( player.pers[ "team" ] == "axis" )
			{
				if ( axis > teamAmount )
				{
					player thread [[ level.allies ]]();
					break;
				}
			}
			else
			{
				if ( axis < teamAmount )
				{
					player thread [[ level.axis ]]();
					break;
				}
				else if ( player.pers[ "team" ] != "allies" )
				{
					player thread [[ level.allies ]]();
					break;
				}
			}
		}
	}
}

/*
	A server thread for monitoring all bot's teams for custom server settings.
*/
teamBots()
{
	for ( ;; )
	{
		wait 1.5;
		
		teamBots_loop();
	}
}

/*
	A server thread for monitoring all bot's in game. Will add and kick bots according to server settings.
*/
addBots_loop()
{
	botsToAdd = getdvarint( "bots_manage_add" );
	
	if ( botsToAdd > 0 )
	{
		setdvar( "bots_manage_add", 0 );
		
		if ( botsToAdd > 64 )
		{
			botsToAdd = 64;
		}
		
		for ( ; botsToAdd > 0; botsToAdd-- )
		{
			level add_bot();
			wait 0.25;
		}
	}
	
	fillMode = getdvarint( "bots_manage_fill_mode" );
	
	if ( fillMode == 2 || fillMode == 3 || fillMode == 5 )
	{
		setdvar( "bots_manage_fill", getGoodMapAmount() );
	}
	
	fillAmount = getdvarint( "bots_manage_fill" );
	
	players = 0;
	bots = 0;
	spec = 0;
	axisplayers = 0;
	alliesplayers = 0;
	
	playercount = level.players.size;
	
	for ( i = 0; i < playercount; i++ )
	{
		player = level.players[ i ];
		
		if ( player is_bot() )
		{
			bots++;
		}
		else if ( !isdefined( player.pers[ "team" ] ) || ( player.pers[ "team" ] != "axis" && player.pers[ "team" ] != "allies" ) )
		{
			spec++;
		}
		else
		{
			players++;
			
			if ( player.pers[ "team" ] == "axis" )
			{
				axisplayers++;
			}
			else if ( player.pers[ "team" ] == "allies" )
			{
				alliesplayers++;
			}
		}
	}
	
	if ( getdvarint( "bots_manage_fill_spec" ) )
	{
		players += spec;
	}
	
	if ( !randomint( 999 ) )
	{
		setdvar( "testclients_doreload", true );
		wait 0.1;
		setdvar( "testclients_doreload", false );
		doExtraCheck();
	}
	
	amount = bots;
	
	if ( fillMode == 0 || fillMode == 2 )
	{
		amount += players;
	}
	
	// use bots as balance
	if ( fillMode == 4 || fillMode == 5 )
	{
		diffPlayers = abs( alliesplayers - axisplayers );
		amount = fillAmount - ( diffPlayers - bots );
		
		if ( players + diffPlayers < fillAmount )
		{
			amount = players + bots;
		}
	}
	
	if ( players <= 0 && getdvarint( "bots_manage_fill_watchplayers" ) )
	{
		amount = fillAmount + bots;
	}
	
	if ( amount < fillAmount )
	{
		setdvar( "bots_manage_add", fillAmount - amount );
	}
	else if ( amount > fillAmount && getdvarint( "bots_manage_fill_kick" ) )
	{
		botsToKick = amount - fillAmount;
		
		if ( botsToKick > 64 )
		{
			botsToKick = 64;
		}
		
		for ( i = 0; i < botsToKick; i++ )
		{
			tempBot = getBotToKick();
			
			if ( isdefined( tempBot ) )
			{
				kick( tempBot getentitynumber(), "EXE_PLAYERKICKED" );
				
				wait 0.25;
			}
		}
	}
}

/*
	A server thread for monitoring all bot's in game. Will add and kick bots according to server settings.
*/
addBots()
{
	level endon( "game_ended" );
	
	bot_wait_for_host();
	
	for ( ;; )
	{
		wait 1.5;
		
		addBots_loop();
	}
}

/*
	When any player spawns
*/
onPlayerSpawned()
{
    self endon( "disconnect" );

    for ( ;; )
    {
        self waittill( "spawned_player" );
        self.gib_ref = undefined;

        if ( self is_bot() )
        {
            self thread apply_zombie_bot_setup();
        }
        else
        {
            // Only play intro sound on the very first spawn of the match
            if ( !isDefined( self.has_played_intro_sound ) )
            {
                self.has_played_intro_sound = true;
                self thread play_spawn_introSound();
            }

            // Start/restart killstreak tracker on EVERY spawn
            self thread monitor_zombie_kills();
        }
    }
}

monitor_zombie_kills()
{
    self endon( "disconnect" );
    self endon( "death" ); // Kills thread when player dies, resetting streak logic

    self.zombie_streak_kills = 0; // Resets kill counter to 0 on new spawn

    for ( ;; )
    {
        // Wait for player to kill an enemy
        self waittill( "killed_enemy" );

        self.zombie_streak_kills++;

        // Reward player every 10 kills
        if ( self.zombie_streak_kills >= 5 )
        {
            self.zombie_streak_kills = 0;
            self thread give_max_ammo_reward();
        }
    }
}

give_max_ammo_reward()
{
    self endon( "disconnect" );

    // 1. Refill ammo for all carried weapons
    weapons = self getWeaponsList();
    for ( i = 0; i < weapons.size; i++ )
    {
        weapon = weapons[i];
        
        // Skip offhand weapons/grenades if desired, or leave to refill everything
        self giveMaxAmmo( weapon );
        self setWeaponAmmoClip( weapon, weaponClipSize( weapon ) );
    }

    // 2. Audio feedback
    self playLocalSound( "mp_level_up" ); // Standard stock MP level up cue

    // 3. On-screen HUD notification
    self thread show_max_ammo_text();
}

show_max_ammo_text()
{
    self endon( "disconnect" );

    if ( isDefined( self.max_ammo_hud ) )
    {
        self.max_ammo_hud destroy();
    }

self.max_ammo_hud = newClientHudElem( self );
    self.max_ammo_hud.elemType = "font";
    self.max_ammo_hud.font = "default";
    self.max_ammo_hud.fontScale = 1.2; // Reduced from 1.8 for smaller text
    self.max_ammo_hud.x = 0;
    self.max_ammo_hud.y = -50;
    self.max_ammo_hud.alignX = "center";
    self.max_ammo_hud.alignY = "middle";
    self.max_ammo_hud.horzAlign = "center";
    self.max_ammo_hud.vertAlign = "middle";
    self.max_ammo_hud.color = ( 1, 1, 1 ); // Pure White
    self.max_ammo_hud.glowColor = ( 0.0, 0.0, 0.0 );
    self.max_ammo_hud.glowAlpha = 0.0;
    self.max_ammo_hud setText( "Kill Streak\nMAX AMMO!" );

    // Fade out effect
    self.max_ammo_hud fadeOverTime( 0.5 );
    self.max_ammo_hud.alpha = 1;
    wait 1.5;
    self.max_ammo_hud fadeOverTime( 1.0 );
    self.max_ammo_hud.alpha = 0;
    wait 1.0;

    if ( isDefined( self.max_ammo_hud ) )
    {
        self.max_ammo_hud destroy();
    }
}

play_spawn_introSound()
{
    self endon( "disconnect" );
    self endon( "death" );

    // Wait 2 frames for audio client initialization
    wait 0.1;

    self playLocalSound( "laugh_child" );
}

set_random_zombie_model()
{
    // Detach old heads/helmets from previous spawns to prevent stacking
    self detachAll();

    // Pool of available German body models
    german_bodies = [];
    german_bodies[0] = "char_ger_honorgd_body2_1";
    german_bodies[1] = "char_ger_honorgd_body1_1";

    // Select a random body model
    chosen_body = german_bodies[ randomInt( german_bodies.size ) ];
    self setModel( chosen_body );

}

apply_zombie_bot_setup()
{
    self endon("disconnect");
    self endon("death");

    wait 0.05;

    // 1. Array of all 25 loaded zombie head xmodels
    heads = [];
    // Zombiehead 1 series
    heads[0]  = "char_ger_honorgd_zombiehead1_1";
    heads[1]  = "char_ger_honorgd_zombiehead1_2";
    heads[2]  = "char_ger_honorgd_zombiehead1_3";
    heads[3]  = "char_ger_honorgd_zombiehead1_4";
    heads[4]  = "char_ger_honorgd_zombiehead1_5";
    heads[5]  = "char_ger_honorgd_zombiehead1_6";
    
    // Zombiehead 2 series
    heads[6]  = "char_ger_honorgd_zombiehead2_1";
    heads[7]  = "char_ger_honorgd_zombiehead2_2";
    heads[8]  = "char_ger_honorgd_zombiehead2_3";
    heads[9]  = "char_ger_honorgd_zombiehead2_4";
    heads[10] = "char_ger_honorgd_zombiehead2_5";
    heads[11] = "char_ger_honorgd_zombiehead2_6";

    // Zombiehead 3 series
    heads[12] = "char_ger_honorgd_zombiehead3_1";
    heads[13] = "char_ger_honorgd_zombiehead3_2";
    heads[14] = "char_ger_honorgd_zombiehead3_3";
    heads[15] = "char_ger_honorgd_zombiehead3_4";
    heads[16] = "char_ger_honorgd_zombiehead3_5";
    heads[17] = "char_ger_honorgd_zombiehead3_6";

    // Zombiehead 4 series
    heads[18] = "char_ger_honorgd_zombiehead4_1";
    heads[19] = "char_ger_honorgd_zombiehead4_2";
    heads[20] = "char_ger_honorgd_zombiehead4_3";
    heads[21] = "char_ger_honorgd_zombiehead4_4";
    heads[22] = "char_ger_honorgd_zombiehead4_5";
    heads[23] = "char_ger_honorgd_zombiehead4_6";

    // Select a random head from the updated array
    random_head = heads[ randomInt( heads.size ) ];

	printf("adding zombie head" + random_head);

    // Clear body and assign Ansel zombie torso
    self detachAll();
    set_random_zombie_model();

    attach_tag = "j_spine4";
    self attach( random_head, attach_tag, true );

    // 3. Give Colt 45 & switch to it
    self giveWeapon("colt45_mp");
    self setSpawnWeapon("colt45_mp");
    self switchToWeapon("colt45_mp");

    // Allow engine 1 frame to mount model before stripping gun mesh
    wait 0.05;

    current_gun = self getCurrentWeapon();
    if ( current_gun != "none" && isDefined( getWeaponModel( current_gun ) ) )
    {
        self detach( getWeaponModel( current_gun ), "tag_weapon_right" );
    }

    // 4. Randomized movement speed scale (creates fast runners vs lumbering zombies)
    // self setMoveSpeedScale( 0.35, 0.7 );

	if ( !isDefined( level.zombie_base_health ) )
    {
        level.zombie_base_health = 25;
    }

    // Assign current scaled health to bot
    self.maxhealth = level.zombie_base_health;
    self.health    = level.zombie_base_health;
    
    // 5. Start pitch lock, lurching, and stumble behavior loops
    self thread freeze_zombie_pitch();
    self thread zombie_lurch_think();
}

freeze_zombie_pitch()
{
    self endon("disconnect");
    self endon("death");

    // Level gaze centered at horizon (0.0 degrees)
    base_pitch = 0.0;

    for ( ;; )
    {
        angles = self getPlayerAngles();
        
        // Sway amplitude (+/- 6 degrees) centered around level pitch
        sway = sin( getTime() * 0.006 ) * 6.0;
        target_pitch = base_pitch + sway;
        
        if ( angles[0] != target_pitch )
        {
            self setPlayerAngles(( target_pitch, angles[1], 0 ));
        }

        wait 0.05;
    }
}

zombie_lurch_think()
{
    self endon("disconnect");
    self endon("death");

    for ( ;; )
    {
        wait randomFloatRange( 0.8, 2.5 );

        // Pick a random side impulse or straight vector
        side_options = [];
        side_options[0] = -60;
        side_options[1] = 60;
        side_options[2] = 0;
        
        side_impulse = side_options[ randomInt( side_options.size ) ];
        
        // Apply temporary lateral movement shift
        self BotBuiltinBotMovement( 127, side_impulse );
        
        wait 0.1;
    }
}

/*
	A thread for ALL players, will monitor and grenades thrown.
*/
onGrenadeFire()
{
	self endon( "disconnect" );
	
	for ( ;; )
	{
		self waittill ( "grenade_fire", grenade, weaponName );
		
		if ( !isdefined( grenade ) )
		{
			continue;
		}
		
		grenade.name = weaponName;
		
		if ( weaponName == "m8_white_smoke_mp" )
		{
			grenade thread AddToSmokeList();
		}
	}
}

/*
	Adds a smoke grenade to the list of smokes in the game. Used to prevent bots from seeing through smoke.
*/
AddToSmokeList()
{
	grenade = spawnstruct();
	grenade.origin = self getorigin();
	grenade.state = "moving";
	grenade.grenade = self;
	
	grenade thread thinkSmoke();
	
	level.bots_smokelist ListAdd( grenade );
}

/*
	The smoke grenade logic.
*/
thinkSmoke()
{
	while ( isdefined( self.grenade ) )
	{
		self.origin = self.grenade getorigin();
		self.state = "moving";
		wait 0.05;
	}
	
	self.state = "smoking";
	wait 11.5;
	
	level.bots_smokelist ListRemove( self );
}

/*
	Waits when the axis uav is called in.
*/
onUAVAxisUpdate()
{
	for ( ;; )
	{
		level waittill( "radar_timer_kill_axis" );
		level thread doUAVUpdate( "axis" );
	}
}

/*
	Waits when the allies uav is called in.
*/
onUAVAlliesUpdate()
{
	for ( ;; )
	{
		level waittill( "radar_timer_kill_allies" );
		level thread doUAVUpdate( "allies" );
	}
}

/*
	Updates the player's radar so bots can know when they have a uav up, because iw3 script is old.
*/
doUAVUpdate( team )
{
	level endon( "radar_timer_kill_" + team );
	
	playercount = level.players.size;
	
	for ( i = 0; i < playercount; i++ )
	{
		player = level.players[ i ];
		
		if ( !isdefined( player.team ) )
		{
			continue;
		}
		
		if ( player.team == team )
		{
			player.bot_radar = true;
		}
	}
	
	wait level.radarviewtime;
	
	playercount = level.players.size;
	
	for ( i = 0; i < playercount; i++ )
	{
		player = level.players[ i ];
		
		if ( !isdefined( player.team ) )
		{
			continue;
		}
		
		if ( player.team == team )
		{
			player.bot_radar = false;
		}
	}
}

/*
	Fixes a weird iw3 bug when for a frame the player doesn't have any bones when they first spawn in.
*/
doPlayerModelFix()
{
	self endon( "disconnect" );
	self waittill( "spawned_player" );
	wait 0.05;
	self.bot_model_fix = true;
}

/*
	A thread for ALL players when they fire.
*/
onWeaponFired()
{
	self endon( "disconnect" );
	self.bots_firing = false;
	
	for ( ;; )
	{
		self waittill( "weapon_fired" );
		self thread doFiringThread();
	}
}

/*
	Lets bot's know that the player is firing.
*/
doFiringThread()
{
	self endon( "disconnect" );
	self endon( "weapon_fired" );
	self.bots_firing = true;
	wait 1;
	self.bots_firing = false;
}

/*
	When a player chats
*/
onPlayerChat()
{
	for ( ;; )
	{
		level waittill( "say", message, player, is_hidden );
		
		for ( i = 0; i < level.bots.size; i++ )
		{
			bot = level.bots[ i ];
			
			bot BotNotifyBotEvent( "chat", "chat", message, player, is_hidden );
		}
	}
}

/*
	Monitors turret usage
*/
turret_monitoruse_watcher()
{
	self endon( "death" );
	
	for ( ;; )
	{
		self waittill ( "trigger", player );
		
		self monitor_player_turret( player );
		
		self.owner = undefined;
		
		if ( isdefined( player ) )
		{
			player.turret = undefined;
		}
	}
}

/*
	While player uses turret
*/
monitor_player_turret( player )
{
	player endon( "death" );
	player endon( "disconnect" );
	
	player.turret = self;
	self.owner = player;
	
	while ( isdefined( player ) && player usebuttonpressed() )
	{
		wait 0.05;
	}
	
	while ( isdefined( player ) && !player usebuttonpressed() )
	{
		wait 0.05;
	}
}
